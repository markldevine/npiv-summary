#!/usr/bin/env raku

use Data::Dump::Tree;
#use Grammar::Debugger;

my %results;

my $jahmc3-lspartition-dlpar = q:to/ENDOFLSPPARTITIONDLPAR/;
<#0> Partition:<17*9080-HEX*78725A8, p595a27.wmata.com, 170.121.1.197>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2919>
<#1> Partition:<150*9080-HEX*78725A8, jaappsprd03.wmata.com, 170.121.12.11>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1454>
<#2> Partition:<253*9080-HEX*78725A8, jadbelmprd01.wmata.com, 10.10.137.22>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1471>
<#3> Partition:<32*9080-HEX*78725A8, , 170.121.1.124>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1226>
<#4> Partition:<255*9080-HEX*78725A8, jatsmprd003.wmata.com, 10.10.137.38>
       Active:<1>, OS:<AIX, 7.3, 7300-01-02-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2661>
<#5> Partition:<152*9080-HEX*78725A8, jaappsprd05.wmata.com, 170.121.12.16>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1536>
<#6> Partition:<9*9080-HEX*78725A8, nimjgb.wmata.com, 170.121.1.194>
       Active:<1>, OS:<AIX, 7.3, 7300-02-01-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1619>
<#7> Partition:<14*9080-HEX*78725A8, p590a1.wmata.com, 170.121.1.201>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1781>
<#8> Partition:<151*9080-HEX*78725A8, jaappsprd04.wmata.com, 170.121.12.12>
       Active:<1>, OS:<AIX, 7.2, 7200-05-07-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1551>
<#9> Partition:<153*9080-HEX*78725A8, jaappsprd06.wmata.com, 170.121.12.13>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1536>
<#10> Partition:<13*9080-HEX*78725A8, p595a13.wmata.com, 170.121.1.120>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1530>
<#11> Partition:<34*9080-HEX*78725A8, p650c4.wmata.com, 170.121.1.50>
       Active:<0>, OS:<AIX, 7.2, 7200-05-07-2346>, DCaps:<0x0>, CmdCaps:<0x0, 0x3b>, PinnedMem:<3959>
<#12> Partition:<212*9080-HEX*78725A8, jatsmprd02.wmata.com, 170.121.1.189>
       Active:<1>, OS:<AIX, 7.3, 7300-02-01-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2066>
<#13> Partition:<100*9080-HEX*78725A8, jaappsprd07.wmata.com, 170.121.70.67>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1405>
<#14> Partition:<7*9080-HEX*78725A8, japowerscprd02.wmata.com, 10.10.137.30>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1454>
<#15> Partition:<25*9080-HEX*78725A8, isplc01.wmata.com, 10.10.137.40>
       Active:<1>, OS:<AIX, 7.3, 7300-01-02-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2220>
<#16> Partition:<5*9080-M9S*025D928, p770sysd1.wmata.com, 170.121.1.76>
       Active:<1>, OS:<AIX, 7.2, 7200-05-07-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2094>
<#17> Partition:<158*9080-HEX*78725A8, , 170.121.12.68>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1529>
<#18> Partition:<157*9080-HEX*78725A8, jaappsprd01.wmata.com, 170.121.12.67>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1404>
<#19> Partition:<278*9080-HEX*78725A8, jaapfareprd28.wmata.com, 170.121.1.116>
       Active:<0>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0x0>, CmdCaps:<0x0, 0x3b>, PinnedMem:<1325>
<#20> Partition:<19*9080-HEX*78725A8, p590a6.wmata.com, 170.121.1.207>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2633>
<#21> Partition:<5*9080-HEX*78725A8, japowerscprd01.wmata.com, 10.10.137.29>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1425>
<#22> Partition:<108*9080-HEX*78725A8, jatsmprd04.wmata.com, 170.121.1.34>
       Active:<1>, OS:<AIX, 7.3, 7300-01-02-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1942>
<#23> Partition:<101*9080-HEX*78725A8, jaapfareprd03.wmata.com, 170.121.70.101>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1242>
<#24> Partition:<6*9080-HEX*78725A8, p590a10.wmata.com, 170.121.1.211>
       Active:<1>, OS:<AIX, 7.2, 7200-05-06-2320>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1809>
<#25> Partition:<1*9080-HEX*78725A8, javio11.wmata.com, 10.10.136.39>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xf4f9f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<13694>
<#26> Partition:<2*9080-HEX*78725A8, javio12.wmata.com, 10.10.136.40>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xf4f9f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<13106>
<#27> Partition:<20*9080-HEX*78725A8, p770a22.wmata.com, 170.121.1.66>
       Active:<0>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0x0>, CmdCaps:<0x0, 0x3b>, PinnedMem:<1303>
<#28> Partition:<18*9080-HEX*78725A8, p595a29.wmata.com, 170.121.1.54>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2606>
<#29> Partition:<254*9080-HEX*78725A8, jadbcgnprd01.wmata.com, 10.10.137.4>
       Active:<1>, OS:<AIX, 7.2, 7200-05-07-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1614>
<#30> Partition:<102*9080-HEX*78725A8, intranet14.wmata.com, 170.121.1.17>
       Active:<1>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1635>
<#31> Partition:<21*9080-HEX*78725A8, p770a23.wmata.com, 170.121.1.67>
       Active:<0>, OS:<AIX, 7.2, 7200-05-05-2246>, DCaps:<0x0>, CmdCaps:<0x0, 0x3b>, PinnedMem:<1252>
<#32> Partition:<256*9080-HEX*78725A8, p520tsmjgb.wmata.com, 10.10.137.41>
       Active:<1>, OS:<AIX, 7.3, 7300-02-01-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1980>
<#33> Partition:<24*9080-M9S*025D928, aixtest.wmata.com, 170.121.1.49>
       Active:<1>, OS:<AIX, 7.2, 7200-05-07-2346>, DCaps:<0xe2c5f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<1280>
<#34> Partition:<2*9080-M9S*025D928, , 10.10.136.46>
       Active:<1>, OS:<AIX, 7.3, 7300-02-01-2346>, DCaps:<0xf4f9f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<3201>
<#35> Partition:<2*9040-MR9*78AE6EX, javio08.wmata.com, 10.10.136.36>
       Active:<0>, OS:<AIX, 7.2, 7200-05-04-2220>, DCaps:<0x0>, CmdCaps:<0x0, 0x0>, PinnedMem:<2663>
<#36> Partition:<1*9040-MR9*78AE6EX, javio07.wmata.com, 10.10.136.35>
       Active:<0>, OS:<AIX, 7.2, 7200-05-04-2220>, DCaps:<0x0>, CmdCaps:<0x0, 0x0>, PinnedMem:<2555>
<#37> Partition:<1*9040-MR9*78AE6FX, javio05.wmata.com, 10.10.136.33>
       Active:<1>, OS:<AIX, 7.2, 7200-05-04-2220>, DCaps:<0xf4f9f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2594>
<#38> Partition:<2*9040-MR9*78AE6FX, javio06.wmata.com, 10.10.136.34>
       Active:<1>, OS:<AIX, 7.2, 7200-05-04-2220>, DCaps:<0xf4f9f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<2536>
<#39> Partition:<1*9080-M9S*025D928, , 10.10.136.45>
       Active:<1>, OS:<AIX, 7.3, 7300-02-01-2346>, DCaps:<0xf4f9f>, CmdCaps:<0x4000003b, 0x3b>, PinnedMem:<3139>
ENDOFLSPPARTITIONDLPAR

my class client-lpar {
    has Int     $.partition-number;
    has Str     $.model;
    has Str     $.type;
    has Str     $.serial-number;
    has Str     $.ip-address;
    has Bool    $.active;
    has Str     $.os-name;
    has Str     $.os-vr;
    has Str     $.os-level;
}


my grammar LSPPARTITIONDLPAR-grammar {
    token TOP {
        ^
        <dlpar-record>+
    }
    token dlpar-record {
        ^^
        <record-number-field>
        \s+
        <partition-field>
        $$ \n
        ^^ \s+
        <active-field>
        <os-field>
        .+?
        $$ \n
    }
    token record-number-field   { '<#' <record-number> '>'          }
    token record-number         { \d+                               }
    token partition-field       {
                                    Partition ':<'
                                    <partition-number>
                                    '*'
                                    <model-type>
                                    '*'
                                    <serial-number>
                                    ',' \s+
                                    <ip-label>*
                                    ',' \s+
                                    <ip-address>
                                    '>'
                                }
    token partition-number      { \d+                               }
    token model-type            { <model> '-' <type>                }
    token model                 { \w ** 4                           }
    token type                  { \w ** 3                           }
    token serial-number         { \w ** 7                           }
    token ip-label              { <-[,]>+                           }
    token ip-address            { <ip-address-octet> ** 4 %% '.'    }
    token ip-address-octet      { \d ** 1..3                        }
    token active-field          { Active ':<' <active> '>,' \s+     }
    token active                { \d                                }
    token os-field              {
                                    OS ':<'
                                    <os-name>
                                    ',' \s+
                                    <os-vr>
                                    ',' \s+
                                    <os-level>
                                    '>'
                                }
    token os-name               { <-[,]>+                           }
    token os-vr                 { <-[,]>+                           }
    token os-level              { <-[\>]>+                          }
}

class LSPPARTITIONDLPAR-actions {
    method dlpar-record ($/) {
        my Int $partition-number    = +$/<partition-field><partition-number>;
        %results{$partition-number} = client-lpar.new:
            :$partition-number,
            :model(~$/<partition-field><model-type><model>),
            :type(~$/<partition-field><model-type><type>),
            :serial-number(~$/<partition-field><serial-number>),
            :ip-address(~$/<partition-field><ip-address>),
            :active(?$/<active-field><active>),
            :os-name(~$/<os-field><os-name>),
            :os-vr(~$/<os-field><os-vr>),
            :os-level(~$/<os-field><os-level>),
        ;

    }
}

LSPPARTITIONDLPAR-grammar.parse($jahmc3-lspartition-dlpar, :actions(LSPPARTITIONDLPAR-actions.new)) || die;
ddt %results;
